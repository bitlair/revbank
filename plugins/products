#!perl
use RevBank::Products qw(read_products);

HELP1 "<productID>" => "Add a product to pending transaction";

sub command :Tab(&tab) ($self, $cart, $command, @) {
    $command =~ /\S/ or return NEXT;
    $command =~ /^\+/ and return NEXT;

    my $products = read_products;

    my $product = $products->{ $command } or return NEXT;
    my $price = $product->{price};

    my $entry = $cart->add(
        -$product->{total_price},
        $product->{description},
        {
            product_id => $product->{id},
            plugin => $self->id,
            product => $product,
            deduplicate => join("/", $self->id, $product->{id}),
        }
    );

    my $contra_desc = "\$you bought $product->{description}";
    my @addons = @{ $product->{addons} // [] };
    my $display = undef;
    $display = "Product"       if @addons and $price->cents > 0;
    $display = "Reimbursement" if @addons and $price->cents < 0;

    $entry->add_contra(
        $product->{contra},
        +$price,
        $contra_desc,
        $display
    );

    for my $addon (@addons) {
        $entry->add_contra(
            $addon->{contra},
            $addon->{price},
            "$addon->{description} ($contra_desc)",
            $addon->{description}
        );
    }

    return ACCEPT;
}

sub tab {
    return grep !/^\+/, grep /\D/, keys %{ read_products() };
}
